/*******************************************************************************
 * MIT License
 *
 * Copyright (c) 2017-2018 Calgary Scientific Incorporated
 *
 * Copyright (c) 2013-2014 kctang
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 ******************************************************************************/
plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.10.0'
}

group = 'com.calgaryscientific.gradle'
version = '0.6.1-SNAPSHOT'

sourceCompatibility = 1.7

dependencies {
    compile localGroovy()
    // This is no longer required since Veracode is now on Maven Central.
    // However, there is no gradle built-in to verify the PGP signature so
    // leaving here in case someone wants to know how to use the local version.
    // compile fileTree(dir: 'lib', include: '*.jar')
    compile("com.veracode.vosp.api.wrappers:vosp-api-wrappers-java:18.6.5.3")
    compile("org.apache.commons:commons-csv:1.5")
    //testCompile gradleTestKit()
    testCompile("cglib:cglib-nodep:3.2.5")
    testCompile("org.spockframework:spock-core:1.1-groovy-2.4") {
        exclude module: "groovy-all"
    }
}

repositories {
    mavenCentral()
}

gradlePlugin {
    plugins {
        veracodePlugin {
            id = 'com.calgaryscientific.gradle.veracode'
            implementationClass = 'com.calgaryscientific.gradle.VeracodePlugin'
        }
    }
}

pluginBundle {
    website = 'https://www.calgaryscientific.com/'
    vcsUrl = 'https://github.com/calgaryscientific/veracode-gradle-plugin'
    tags = ['veracode', 'security']
    plugins {
        veracodePlugin {
            displayName = 'Veracode Gradle Plugin'
            description = """\
                Veracode Gradle Plugin that automates Veracode application security scanning activities
                 https://github.com/calgaryscientific/veracode-gradle-plugin""".stripIndent()
        }
    }
}

publishing {
    repositories {
        maven {
            name = 'testRepo'
            url = uri("${buildDir}/repo")
        }
    }
    publications {
        // Ensure transitive dependencies are added to POM file
        mavenJava(MavenPublication) {
            from project.components.java
        }
    }
}
